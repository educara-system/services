buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.4.+"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.7"
    }
    repositories() {
        jcenter()
    }
}
plugins {
    id 'idea'
    id 'org.sonarqube' version '2.2.1'
    id 'com.jfrog.artifactory' version '4.4.7'
    id 'maven-publish'
}

def cukeVersion = '1.2.+'
def powermockVersion = '1.6.6'
def springVersion = '1.4.+'
def swaggerVersion = '1.5.+'
def springFoxVersion = '2.5.+'
def swaggerCodegenVersion = '2.+'
def stateMachineVersion = '1.2.+'
def rabbitVersion = '1.+'

group 'com.educara.student'

allprojects {
    repositories {
 		jcenter()
 	}
}
subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.compilerArgs += '-Xlint'
        options.compilerArgs += '-Werror'
    }

    test.finalizedBy jacocoTestReport

    jacocoTestReport {
        reports {
            html.enabled = true
        }
    }
    version = '1.0.0-SNAPSHOT'
}
project(':') {

    apply plugin: 'base'

    task cleanGeneratedDirectory(type: Delete) {
        delete "$projectDir/student-api/generated"
    }

    task copySwaggerCodegen(type: Copy) {
        configurations {
            swagger
        }

        dependencies {
            swagger group: 'io.swagger', name: 'swagger-codegen-cli', version: swaggerCodegenVersion
        }

        into "build"
        from configurations.swagger {
            rename "swagger-codegen-cli-.*.jar", "swagger-codegen.jar"
        }
    }
    // This task converts a swagger yaml file to a swagger json file using the swagger-codegen tool.
    // This is only necessary due to a limitation of the swagger_spec_validator python module which doesn't work on
    // remote yaml files.  This can be removed when we stop using Python for the REST front-end to C++ services.
    task convertToJson(type: Exec, dependsOn: copySwaggerCodegen) {
        commandLine "bash", "-c", "java -jar build/swagger-codegen.jar generate -i student.yaml -l swagger && mv swagger" +
                ".json student.json"
    }

    clean.dependsOn(cleanGeneratedDirectory)

    task runsonar {
        doLast {
            println "Sonar Report link : " + project.buildDir + "/sonar/issues-report/issues-report.html"
        }
    }
    project.tasks["runsonar"].dependsOn "sonarqube"

    sonarqube {
        properties {
            property "sonar.exclusions", "**/generated/src/main/java/**"
        }
    }
}

project(':student-api') {

    sourceSets {
        main {
            java {
                srcDir "$projectDir/generated/src/main/java"
            }
        }
    }

    configurations {
        swagger
    }

    dependencies {
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springVersion
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat', version: springVersion
        compile group: 'io.swagger', name: 'swagger-annotations', version: swaggerVersion
        compile group: 'io.springfox', name: 'springfox-swagger2', version: springFoxVersion
        swagger group: 'io.swagger', name: 'swagger-codegen-cli', version: swaggerCodegenVersion
    }
    task copySwaggerCodegen(type: Copy) {
        into "$buildDir"
        from configurations.swagger {
            rename "swagger-codegen-cli-.*.jar", "swagger-codegen.jar"
        }
    }

}
project(':student-impl') {
    dependencies {
        compile project(':student-api')
        compile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: springVersion
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springVersion
        compile group: 'org.springframework.statemachine', name: 'spring-statemachine-core', version: stateMachineVersion
        testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
        testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: powermockVersion
        testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: powermockVersion
        testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springVersion
        testCompile group: 'info.cukes', name: 'cucumber-java8', version: cukeVersion
        testCompile group: 'info.cukes', name: 'cucumber-junit', version: cukeVersion
        testCompile group: 'info.cukes', name: 'cucumber-spring', version: cukeVersion
    }
}

project(':student-ws') {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'war'

    dependencies {
        compile project(':student-api')
        compile project(':student-impl')
        compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-stream-rabbit', version: rabbitVersion
        compile group: 'io.springfox', name: 'springfox-swagger2', version: springFoxVersion
        compile group: 'io.springfox', name: 'springfox-swagger-ui', version: springFoxVersion
        compile group: 'org.springframework.boot', name: 'spring-boot-legacy', version: '1.0.2.RELEASE'
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springVersion
        compile(group: 'org.springframework.boot', name: 'spring-boot-starter', version: springVersion) {
            exclude(module: 'commons-logging')
        }
        testCompile project(':student-api')
        testCompile project(':student-impl')
        testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: powermockVersion
        testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: powermockVersion
        testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springVersion) {
            exclude(module: 'commons-logging')
        }
        testCompile group: 'info.cukes', name: 'cucumber-java8', version: cukeVersion
        testCompile group: 'info.cukes', name: 'cucumber-junit', version: cukeVersion
        testCompile group: 'info.cukes', name: 'cucumber-spring', version: cukeVersion
    }
}